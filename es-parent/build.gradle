buildscript {
    dependencies {
        classpath("org.liquibase:liquibase-core:5.0.1")
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.liquibase.gradle' version '2.2.2'
}

description = 'parent'

allprojects {
    group = 'lynx.com'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        if (project.name != 'common' && project.name != 'parent') {
            dependencies {
                implementation project(':common')
            }
        }

        if (project.name == 'api') {
            dependencies {
                implementation project(':application')
            }
        }

        if (project.name == 'application') {
            dependencies {
                implementation project(':domain')
            }
        }

        if (project.name == 'infrastructure') {
            dependencies {
                implementation project(':domain')
                implementation project(':application')
            }
        }

        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.mapstruct:mapstruct:1.5.5.Final'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        runtimeOnly 'org.postgresql:postgresql'
        developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}


// Configure subprojects to build plain jars (not Boot jars)
subprojects {
    tasks.named('bootJar') {
        enabled = false
    }
    tasks.named('jar') {
        enabled = true
    }
}


dependencies {
    implementation 'org.liquibase:liquibase-core'
    implementation project(':api')
    implementation project(':application')
    implementation project(':domain')
    implementation project(':infrastructure')

    liquibaseRuntime 'org.liquibase:liquibase-core:5.0.1'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:4.0.1'
    liquibaseRuntime 'info.picocli:picocli:4.6.1'
    liquibaseRuntime 'org.postgresql:postgresql:42.7.2'
    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate6:5.0.1'
    liquibaseRuntime 'org.springframework:spring-beans:6.2.0'
    liquibaseRuntime 'org.springframework:spring-context:6.2.0'
    liquibaseRuntime 'org.springframework:spring-core:6.2.0'
    liquibaseRuntime 'org.springframework.data:spring-data-jpa'
    liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'
    liquibaseRuntime 'jakarta.persistence:jakarta.persistence-api:3.2.0'
    liquibaseRuntime 'org.hibernate.orm:hibernate-core:6.6.3.Final'
    liquibaseRuntime 'net.bytebuddy:byte-buddy:1.15.10'
    liquibaseRuntime 'org.javassist:javassist:3.30.2-GA'
    liquibaseRuntime 'javax.xml.bind:jaxb-api:2.3.1'
    liquibaseRuntime sourceSets.main.output
    liquibaseRuntime project(':infrastructure').sourceSets.main.output
}

liquibase {
    activities {
        main {
            changelogFile 'db/changelog/db.changelog-master.yaml'
            url 'jdbc:postgresql://localhost:5432/postgres'
            username 'postgres'
            password 'postgres'
            driver 'org.postgresql.Driver'
            searchPath 'src/main/resources,../es-infrastructure/src/main/resources'
            classpath 'src/main/resources,../es-infrastructure/src/main/resources'
        }
        generateChangelog {
            changelogFile "src/main/resources/db/changelog/changes/generated-${new Date().format('yyyyMMdd-HHmmss')}.yaml"
            url 'jdbc:postgresql://localhost:5432/postgres'
            username 'postgres'
            password 'postgres'
            driver 'org.postgresql.Driver'
            referenceUrl 'hibernate:spring:lynx.com?dialect=org.hibernate.dialect.PostgreSQLDialect'
            referenceDriver 'liquibase.ext.hibernate.database.connection.HibernateDriver'
        }
        diffChangeLog {
            changelogFile "src/main/resources/db/changelog/changes/diff-${new Date().format('yyyyMMdd-HHmmss')}.yaml"
            url 'jdbc:postgresql://localhost:5432/postgres'
            username 'postgres'
            password 'postgres'
            driver 'org.postgresql.Driver'
            referenceUrl 'hibernate:spring:lynx.com?dialect=org.hibernate.dialect.PostgreSQLDialect&hibernate.physical_naming_strategy=org.hibernate.boot.model.naming.CamelCaseToUnderscoresNamingStrategy'
            referenceDriver 'liquibase.ext.hibernate.database.connection.HibernateDriver'
            defaultSchemaName 'public'
        }
    }
    runList = project.hasProperty('runList') ? project.property('runList') : 'main'
}

tasks.named('bootJar') {
    enabled = true
}



