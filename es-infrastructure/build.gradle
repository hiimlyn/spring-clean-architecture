/*
 * INFRASTRUCTURE MODULE - JPA, Database, External Systems
 * 
 * RULES:
 * - Contains JPA entities and repositories
 * - Contains external system integrations
 * - Implements ports defined in application module
 * - Depends on both application and domain modules
 * - This is the ONLY module with JPA/database access
 * - Contains Liquibase for database migrations
 * 
 * DEPENDENCY: infrastructure â†’ application, domain
 * 
 * This module implements the interfaces (ports) defined
 * in the application layer. API module depends on this.
 */

buildscript {
    dependencies {
        classpath("org.liquibase:liquibase-core:5.0.1")
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.liquibase.gradle' version '2.2.2'
}

description = 'infrastructure - JPA, database, external systems'

dependencies {
    // Application module (which transitively exposes domain via 'api')
    implementation project(':application')
    
    // Direct domain access for entity mapping
    implementation project(':domain')
    
    // Common utilities
    implementation project(':common')
    
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    
    // Liquibase for database migrations
    implementation 'org.liquibase:liquibase-core'
    
    // Database
    runtimeOnly 'org.postgresql:postgresql'
    
    // MapStruct for entity-domain mapping
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    
    // Lombok for JPA entities
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    
    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    // Liquibase Gradle plugin runtime dependencies
    liquibaseRuntime 'org.liquibase:liquibase-core:5.0.1'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:4.0.1'
    liquibaseRuntime 'info.picocli:picocli:4.6.1'
    liquibaseRuntime 'org.postgresql:postgresql:42.7.2'
    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate6:5.0.1'
    liquibaseRuntime 'org.springframework:spring-beans:6.2.0'
    liquibaseRuntime 'org.springframework:spring-context:6.2.0'
    liquibaseRuntime 'org.springframework:spring-core:6.2.0'
    liquibaseRuntime 'org.springframework.data:spring-data-jpa'
    liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'
    liquibaseRuntime 'jakarta.persistence:jakarta.persistence-api:3.2.0'
    liquibaseRuntime 'org.hibernate.orm:hibernate-core:6.6.3.Final'
    liquibaseRuntime 'net.bytebuddy:byte-buddy:1.15.10'
    liquibaseRuntime 'org.javassist:javassist:3.30.2-GA'
    liquibaseRuntime 'javax.xml.bind:jaxb-api:2.3.1'
    liquibaseRuntime sourceSets.main.output
}

// =============================================================================
// LIQUIBASE CONFIGURATION
// =============================================================================
liquibase {
    activities {
        main {
            changelogFile 'db/changelog/db.changelog-master.yaml'
            url 'jdbc:postgresql://localhost:5432/postgres'
            username 'postgres'
            password 'postgres'
            driver 'org.postgresql.Driver'
            searchPath 'src/main/resources'
            classpath 'src/main/resources'
        }
        generateChangelog {
            changelogFile "src/main/resources/db/changelog/changes/generated-${new Date().format('yyyyMMdd-HHmmss')}.yaml"
            url 'jdbc:postgresql://localhost:5432/postgres'
            username 'postgres'
            password 'postgres'
            driver 'org.postgresql.Driver'
            referenceUrl 'hibernate:spring:lynx.com?dialect=org.hibernate.dialect.PostgreSQLDialect'
            referenceDriver 'liquibase.ext.hibernate.database.connection.HibernateDriver'
        }
        diffChangeLog {
            changelogFile "src/main/resources/db/changelog/changes/diff-${new Date().format('yyyyMMdd-HHmmss')}.yaml"
            url 'jdbc:postgresql://localhost:5432/postgres'
            username 'postgres'
            password 'postgres'
            driver 'org.postgresql.Driver'
            referenceUrl 'hibernate:spring:lynx.com?dialect=org.hibernate.dialect.PostgreSQLDialect&hibernate.physical_naming_strategy=org.hibernate.boot.model.naming.CamelCaseToUnderscoresNamingStrategy'
            referenceDriver 'liquibase.ext.hibernate.database.connection.HibernateDriver'
            defaultSchemaName 'public'
        }
    }
    runList = project.hasProperty('runList') ? project.property('runList') : 'main'
}

// Disable bootJar - infrastructure is a library, not the main app
tasks.named('bootJar') {
    enabled = false
}

tasks.named('jar') {
    enabled = true
}